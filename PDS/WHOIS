WHOIS    TITLE 'WHOIS -- TSO USERID COMMAND'
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
**********************************************************************
*                                                                    *
*  MODULE NAME -                                                     *
*                                                                    *
*     WHOIS  -- TSO COMMAND PROCESSOR TO DISPLAY THE "NAME"          *
*               FIELD OF A RACF USERID AT THE TERMINAL.              *
*                                                                    *
*  FUNCTION -                                                        *
*                                                                    *
*     SEE ABOVE...                                                   *
*                                                                    *
*  ATTRIBUTES -                                                      *
*                                                                    *
*     NONREENTRANT, NONREUSABLE, NONREFRESHABLE                      *
*     MUST RUN AMODE / RMODE 24                                      *
*                                                                    *
*  REQUIREMENTS -                                                    *
*                                                                    *
*     -  'WHOIS' MUST BE ENTERED IN THE RACF AUTHORIZED              *
*            CALLER TABLE (ICHAUTAB)                                 *
*                                                                    *
*     -  'WHOIS' SHOULD BE ENTERED INTO THE TSO/E AUTHORIZED         *
*            COMMAND TABLE (IKJEFTE2)                                *
*                                                                    *
*     -  LOAD MODULE MUST BE LINKED 'AC=1' AND PLACED INTO           *
*            AN APF AUTHORIZED LIBRARY                               *
*                                                                    *
*     -  THE RACF USERID(S) MUST BE >1  CHARACTERS IN LENGTH         *
*         CHANGED BY DXI                                             *
*                                                                    *
*  CHANGE HISTORY -                                                  *
*                                                                    *
*     02/17/13, REMOVED THE 6 CHARACTER RESTRICTION                  *
*                                                                    *
*     03/11/87, REMOVED THE CODE FOR 'SVC 236' THAT KARL TAI HAD     *
*               WRITTEN, WILL USE THE RACF AUTHORIZED CALLER TABLE   *
*               INSTEAD AS IT SHOULD BE...ELL                        *
*                                                                    *
**********************************************************************
WHOIS    CSECT
WHOIS    AMODE 24
WHOIS    RMODE 24
         STM   14,12,12(13)       SAVE CALLERS REGISTERS
         BALR  12,0               SET CURRENT ADDRESS
         USING *,12               ESTABLISH ADDRESSABILITY
         ST    13,SAVE+4          CHAIN SAVE AREA
         LR    11,13              POINT TO CALLERS SAVE AREA
         LA    13,SAVE            POINT TO OUR SAVE AREA
         ST    13,8(11)           CHAIN SAVE AREA
         SPACE
         USING CPPL,R1            ADDRESS CPPL
         L    R2,CPPLCBUF         POINT TO COMMAND BUFFER
         LA   R3,4(R2)             POINT TO START OF INPUT TEXT
         AH   R3,2(R2)             POINT TO USERID ENTERED
         AH    R2,0(R2)             POINT TO END OF COMMAND BUFFER
         SR    R2,R3                POINT TO PARAMETER ENTERED
         CH    R2,=H'1'            WAS > 1 CHARACTER PARAMTER ENTERED?
         BH    PARMOK1            ..YES, CONTINUE 1 AND 8
         B     PARMFAIL
PARMOK1  DS    0H
         CH    R2,=H'9'            WAS > 1 CHARACTER PARAMTER ENTERED?
         BL    PARMOK             ..YES, CONTINUE
PARMFAIL TPUT  MSG1,L'MSG1        WRITE ERROR MESSGAE
         B     RETURN             RETURN TO CALLER
         EJECT
PARMOK   DS    0H
         STH  R2,LENFLD
         BCTR R2,0                DOWN 1 FOR EXECUTE MOVE
         EX    R2,EXMVC           MOVE PARAMETER TO RACF PARMLIST
         EX    R2,EXOC            UPPER CASE
         L     R3,16               POINT TO CVT
         USING CVT,R3
         L     R3,CVTTCBP          POINT TO TCB AND ASCB POINTERS
         L     R3,4(R3)             POINT TO CURRENT TCB
         EJECT
AUTHOK   DS    0H
         RACSTAT
         LTR   R15,R15              IS RACF OPERATING?
         BZ    RACFOK             ..YES, CONTINUE
         TPUT  MSG2,L'MSG2        WRITE ERROR MESSGAE
         B     RETURN             RETURN TO CALLER
         EJECT
RACFOK   ICHEINTY LOCATE,         LOCATE NEXT USERID                   X
               TYPE='USR',ENTRY=ENTRY,WKAREA=WKAREA,                   X
               ACTIONS=(ACTION1)
         LTR   R15,R15              WAS LOCATE SUCCESSFUL?
         BZ    FOUND              ..YES, CONTINUE
         C     R15,=F'12'          WAS LOCATE UNSUCCESSFUL?
         BE    NOTFOUND           ..YES, CONTINUE
         CVD   15,WORKD           CONVERT RETURN CODE TO DECIMAL
         UNPK  ERRORCD,WORKD+6(2) MOVE TO ERROR MESSAGE
         OI    ERRORCD+1,X'F0'    MAKE LAST CHARACTER PRINTABLE
         TPUT  MSG5,L'MSG5        WRITE ERROR MESSAGE
         B     RETURN             RETURN TO CALLER
         EJECT
FOUND    MVC   MSG3,NAME          MOVE TO MESSAGE
         TPUT  MSG3,L'MSG3        WRITE MESSAGE
         B     RETURN             RETURN TO CALLER
NOTFOUND DS    0H                 WRITE NOFOUND MESSAGE
         MVC   MSG4(8),USERID
         TPUT  MSG4,L'MSG4        WRITE NOFOUND MESSAGE
         B     RETURN             RETURN TO CALLER
         EJECT
RETURN   L     13,SAVE+4          POINT TO CALLERS SAVE AREA
         RETURN (14,12),RC=0      RETURN TO CALLER
         EJECT
SAVE     DS    18F                SAVE AREA
WORKD    DS    D                  DOUBLEWORD WORK AREA
* PARM FIELD
LENFLD   DS    0H
         DS    AL1    FILLER
*   BEGINS HERE ...
ENTRY    DC    AL1(0)             LENGTH OF USERID FIELD
USERID   DC    XL8'00'            USERID
*   ENDS
BLANK8   DC    CL8'        '      8 BLANKS
EXMVC    MVC   USERID(0),0(R3)
EXOC     OC    USERID(0),BLANK8
         DS    0F
WKAREA   DC    AL4(WKAREAL)       LENGTH OF WORKAREA
RBA      DS    CL6                RBA OF RACF RECORD
FLAGS    DS    X                  FLAGS
         DS    C                  RESERVED
DUPCOUNT DS    AL4                DUPLICATE RECORD COUNT
         DS    CL8                RESERVED
RETLEN   DS    AL4                LENGTH OF RETURNED DATA
NAMEL    DS    AL2
NAME     DS    CL20
WKAREAL  EQU   *-WKAREA           CALCULATE WORK AREA LENGTH
MSG1     DC    C'USERID NOT VALID - MUST BE > 1 < 9  CHARACTERS'
MSG2     DC    C'RACF IS NOT OPERATING'
MSG3     DC    CL20' '
MSG4     DC    C'         NOT FOUND'
MSG5     DC    C'RACF ERROR, RETURN CODE FROM ICHEINTY MACRO = XX'
         ORG   MSG5+46
ERRORCD  DS    CL2                ERROR RETURN CODE
         ORG
         LTORG
         EJECT
ACTION1  ICHEACTN FIELD=PGMRNAME
         EJECT
         PRINT NOGEN
         IKJCPPL
         CVT DSECT=YES
         IKJTCB
         END
