IEFUJI   TITLE 'MVS SMF JOB INITIATION EXIT'                            00001**2
IEFUJI   CSECT                                                          00002**2
* FUNCTION                                                              00003**2
*                                                                       00004**2
* THE PURPOSE OF THIS ROUTINE IS TO PASS USERID AND ACCOUNTING          00005**2
* INFORMATION TO THE TMS ACCOUNTING ROUTINE TMSACTRT.                   00006**2
* THE TMS USER ACCOUNTING FIELD IN THE TMC RECORD IS 41 BYTES.          00007**2
* ALL TMS ROUTINES ASSUME 41 BYTES ARE USED. THIS ROUTINE               00008**2
* PLACES RACFID IN 1-6 FOLLOWED BY AN 8 BYTE FIELD IN 7-14              00009**2
* OF ACCOUNTING INFO FROM THE SMFJMR FOR A TOTAL 14 BYTES USED.         00010**2
*                                                                       00011**2
* INPUT                                                                 00012**2
*                                                                       00013**2
*    REGISTER 1 POINTS TO A LIST OF 4 BYTE ADDRESSES FOR THE            00014**2
*    FOLLOWING FOUR PARAMETERS:                                         00015**2
*       1 - JOB MANAGEMENT RECORD                                       00016**2
*       2 - 20 BYTES CONTAINING THE PROGRAMMERS NAME                    00017**2
*       3 - 1 BYTE INDICATING THE EFFECTIVE JOB PRIORITY                00018**2
*       4 - JOB ACCOUNTING FIELDS                                       00019**2
*                                                                       00020**2
*                                                                       00021**2
* OUTPUT                                                                00022**2
*                                                                       00023**2
*    REGISTER 15 WILL CONTAIN  THE FOLLOWING RETURN CODE:               00024**2
*       0 - CONTINUE PROCESSING THE JOB                                 00025**2
*                                                                       00026**2
* ENTRY POINTS                                                          00027**2
*                                                                       00028**2
*    IEFUJI                                                             00029**2
*                                                                       00030**2
* EXTERNAL REFERENCES                                                   00031**2
*                                                                       00032**2
*    TMSACTRT - VIA LINK                                                00033**2
*    NOTE: TMACTRT SHOULD BE RESIDENT IN THE LINK PACK AREA             00034**2
*                                                                       00035**2
* EXITS, NORMAL - RETURN TO INITIATOR WITH A RETURN CODE OF ZERO        00036**2
*                                                                       00037**2
* ATTRIBUTES                                                            00038**2
*    REENTRANT (REQUIRED FOR LINK PACK AREA RESIDENCY)                  00039**2
*    AC=1 (REQUIRED FOR ISSUANCE OF RACXTRT MACRO)                      00040**2
*********************************************************************** 00041**2
*  CHANGE HISTORY                                                       00042**2
*    CREATED 05/22/86                                                   00043**2
*                                                                       00044**2
*********************************************************************** 00045**2
         SPACE 3                                                        00046**2
*********************************************************************** 00047**2
*                                                                     * 00048**2
*                 REGISTER EQUATES                                    * 00049**2
*                                                                     * 00050**2
*********************************************************************** 00051**2
         SPACE 3                                                        00052**2
R0       EQU   0                                                        00053**2
R1       EQU   1                                                        00054**2
R2       EQU   2                                                        00055**2
R3       EQU   3                                                        00056**2
R4       EQU   4                                                        00057**2
R5       EQU   5                                                        00058**2
R6       EQU   6                                                        00059**2
R7       EQU   7                                                        00060**2
R8       EQU   8                                                        00061**2
R9       EQU   9                                                        00062**2
R10      EQU   10                                                       00063**2
R11      EQU   11                                                       00064**2
R12      EQU   12                                                       00065**2
R13      EQU   13                                                       00066**2
R14      EQU   14                                                       00067**2
R15      EQU   15                                                       00068**2
         EJECT                                                          00069**2
         SAVE  (14,12),,*     STANDARD LINKAGE                          00070**2
         SPACE 1                                                        00071**2
         LR    R12,R15                                                  00072**2
         USING IEFUJI,R12                                               00073**2
         LR    R4,R1          SAVE PARAMETER LIST IN R4                 00074**2
         SPACE 1                                                        00075**2
         GETMAIN  R,LV=SAVELNTH,SP=245   #2  SPECIFY SUBPOOL 245        00076**2
         SPACE 1                                                        00077**2
         LR    R3,R13         RETAIN ADDRESS OF HIGHER SAVE AREA IN R3  00078**2
         LR    R13,R1                                                   00079**2
         ST    R13,8(R3)                                                00080**2
         ST    R3,4(R13)                                                00081**2
         USING SAVEAREA,R13                                             00082**2
         SPACE 1                                                        00083**2
         XC    16(4,R3),16(R3)  SET DEFAULT RETURN CODE OF ZERO         00084**2
         SPACE 1                                                        00085**2
*  GO GET THE ACCOUNTING INFORMATION AND RACFID                         00086**2
         USING SMFPARAM,R4            DSECT OF INPUT PARAMETERS         00087**2
         L     R2,SMFACCT             ADDRESS OF JOB ACCOUNTING DATA    00088**2
         MVC   TMSACTNC(8),=CL8'00000000' DEFAULT TO ZEROS              00089**2
         LA    R5,41                  SET MAX MOVE AT 41 BYTES          00090**2
         CLI   1(R2),X'00'            IS LENGTH ZERO                    00091**2
         BE    USERIDM                NO, GO FOR VAR NEW MOVE           00092**2
         CLI   1(R2),X'29'            TMS MAXIMIM LENGTH IS 41 BYTES    00093**2
         BH    EXMVC                  IF HIGHER THEN LIMIT IT           00094**2
         LA    R5,0                   ZERO R5                           00095**2
         IC    R5,1(R2)               INSERT LENGTH CODE                00096**2
         BCTR  R5,0                   DECREMENT BY 1                    00097**2
EXMVC    EX    R5,MVCACT              MOVE NEW VAR ACT NUMBER           00098**2
USERIDM  DS    0H                                                       00099**2
         RACXTRT TYPE=EXTRACT,SUBPOOL=245 ASK RACF FOR PROFILE OF ACEE  00100**2
         LTR   R15,R15                IF R15 NOT ZERO THEN I CANT       00101**2
         BNZ   RETURN                 FREEMAIN OR DO TMSACTRT LINK      00102**2
         MVC   TMSUSERI,24(R1)        MOVE IN RACFID                    00103**2
         L     R0,0(R1)               SET SUBPOOL AND LENGTH            00104**2
         FREEMAIN  R,LV=(0),A=(1)     FREEMAIN THAT AREA                00105**2
*                                                                       00106**2
         LA    R1,72(R13)             ADDRESS OF TMSACCT IN R1          00107**2
*                                    ADDRESS OF SAVE AREA ALREADY IN 13 00108**2
         LINK EP=TMSACTRT             PASS CONTROL TO TMASCTRT          00109**2
         SPACE 1                                                        00110**2
RETURN   DS    0H                                                       00111**2
         LR    R1,R13                                                   00112**2
         L     R13,4(R13)                                               00113**2
         FREEMAIN  R,LV=SAVELNTH,SP=245,A=(1)  #2  SPECIFY SUBPOOL 245  00114**2
         RETURN  (14,12),T                                              00115**2
         EJECT                                                          00116**2
         SPACE 3                                                        00117**2
MVCACT   MVC   TMSACTNC(0),2(R2)                                        00118**2
         LTORG                                                          00119**2
SAVEAREA DSECT                                                          00120**2
         SPACE 1                                                        00121**2
         DS    18F            STANDARD SAVE AREA                        00122**2
         SPACE 1                                                        00123**2
         TMACCT               TMS ACCT AREA DSECT                       00124**2
         SPACE 1                                                        00125**2
SAVEEND  EQU   *                                                        00126**2
SAVELNTH EQU   SAVEEND-SAVEAREA                                         00127**2
         EJECT                                                          00128**2
         SPACE 3                                                        00129**2
*        DSECT OF INPUT PARAMETER LIST POINTED TO BY REG 1              00130**2
         SPACE                                                          00131**2
SMFPARAM DSECT                        INPUT PARAMETER LIST              00132**2
SMFJMR   DS    A                      ADDRESS OF SMF PARAM AREA         00133**2
SMFPROG  DS    A                      ADDRESS OF PROGRAMMER NAME        00134**2
SMFPRI   DS    A                      ADDRESS OF JOB SELECTION PRIORITY 00135**2
SMFACCT  DS    A                      ADDRESS OF JOB ACCOUNTING DATA    00136**2
*                                       FORMAT OF JOB ACCOUNTING DATA:  00137**2
*                                         0 - NUMBER OF ACCOUNT FIELDS  00138**2
*                                         1 - LENGTH OF FIRST FIELD     00139**2
*                                         2 - FIRST ACCOUNT FIELD       00140**2
*                                         ETC.                          00141**2
         END                                                            00142**2
